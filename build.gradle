plugins {
    id("org.sonarqube") version "3.3"
    id("java")
    id("com.github.johnrengelman.shadow") version "7.0.0"
    id("jacoco")
    id("io.freefair.lombok") version "6.1.0-m3"
}

group "pink.zak.giveawaybot"
version "0.1.0"

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

repositories {
    mavenCentral()
    mavenLocal()
    maven { url("https://m2.dv8tion.net/releases") }
    maven { url("https://repo.codemc.org/repository/maven-public/") }
    maven { url("https://oss.sonatype.org/content/repositories/snapshots/") }
    maven { url("https://jitpack.io") }
    maven { url("https://zoidberg.ukp.informatik.tu-darmstadt.de/artifactory/public-releases/") } // trove
}

dependencies {
    implementation("net.dv8tion:JDA:4.3.0_303") { exclude module: "opus-java" }
    implementation("com.timvisee:yamlwrapper:1.2.0")
    implementation("com.vdurmont:emoji-java:5.1.1")

    implementation("ch.qos.logback:logback-classic:1.2.5")
    implementation("com.github.ZakShearman:influx-metrics:1.1.0")
    implementation("com.influxdb:influxdb-client-java:4.0.0")
    implementation("me.tongfei:progressbar:0.9.2")

    implementation("com.google.guava:guava:30.1.1-jre")
    implementation("org.mongodb:mongo-java-driver:3.12.9")
    implementation("org.mongojack:mongojack:4.2.0")

    testImplementation("org.junit.jupiter:junit-jupiter-api:5.7.2")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.7.2")
    testImplementation("org.assertj:assertj-core:3.20.2")
    testImplementation("org.awaitility:awaitility:4.1.0")
    testImplementation("gnu.trove:trove:3.0.3")
}

shadowJar {
    manifest {
        attributes "Class-Path": "pink.zak.giveawaybot",
                "Main-Class": "pink.zak.giveawaybot.Main",
                "Implementation-Title": "SmartGiveaways Bot",
                "Implementation-Version": this.version,
                "Implementation-Vendor": "SmartGiveaways"
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "SmartGiveaways_smart-giveaway-bot"
        property "sonar.organization", "smartgiveaways"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}

task depsize {
    doLast {
        final formatStr = "%,10.2f"
        final conf = configurations.default
        final size = conf.collect { it.length() / (1024 * 1024) }.sum()
        final out = new StringBuffer()
        out << "Total dependencies size:".padRight(45)
        out << "${String.format(formatStr, size)} Mb\n\n"
        conf.sort { -it.length() }
                .each {
                    out << "${it.name}".padRight(45)
                    out << "${String.format(formatStr, (it.length() / 1024))} kb\n"
                }
        println(out)
    }
}
