buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:6.1.0"
    }
}

plugins {
    id("org.sonarqube") version "3.0"
    id("java")
    id("com.github.johnrengelman.shadow") version "6.1.0"
    // Spring
    id("org.springframework.boot") version "2.4.1"
    id("io.spring.dependency-management") version "1.0.10.RELEASE"
}

group "pink.zak.giveawaybot"
version "0.1.0"

sourceCompatibility = JavaVersion.VERSION_15
targetCompatibility = JavaVersion.VERSION_15

allprojects {
    apply plugin: 'java'

    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
        maven { url("https://repo.codemc.org/repository/maven-public/") }
        maven { url("https://oss.sonatype.org/content/repositories/snapshots/") }
        maven { url("https://jitpack.io") }
    }

    dependencies {
        implementation("com.google.guava:guava:30.1-jre")
        implementation("org.mongodb:mongo-java-driver:3.12.7")
        implementation("org.mongojack:mongojack:4.0.2")

        compileOnly("org.projectlombok:lombok:1.18.16")
        annotationProcessor("org.projectlombok:lombok:1.18.16")

        testImplementation("org.junit.jupiter:junit-jupiter-api:5.7.0")
        testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.7.0")
        testImplementation("org.assertj:assertj-core:3.18.1")
    }

    tasks.withType(JavaCompile).all {
        options.compilerArgs += ["--enable-preview"]
    }

    test {
        useJUnitPlatform()
        jvmArgs(['--enable-preview'])
    }
}

dependencies {
    implementation project(":api")
    implementation project(":discord")
    implementation("org.springframework.boot:spring-boot-starter-web")
}

shadowJar {
    manifest {
        attributes "Class-Path": "pink.zak.giveawaybot",
                "Main-Class": "pink.zak.giveawaybot.Main",
                "Implementation-Title": "SmartGiveaways Bot",
                "Implementation-Version": this.version,
                "Implementation-Vendor": "SmartGiveaways"
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "SmartGiveaways_smart-giveaway-bot"
        property "sonar.organization", "smartgiveaways"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

task depsize {
    doLast {
        final formatStr = "%,10.2f"
        final conf = configurations.default
        final size = conf.collect { it.length() / (1024 * 1024) }.sum()
        final out = new StringBuffer()
        out << "Total dependencies size:".padRight(45)
        out << "${String.format(formatStr, size)} Mb\n\n"
        conf.sort { -it.length() }
                .each {
                    out << "${it.name}".padRight(45)
                    out << "${String.format(formatStr, (it.length() / 1024))} kb\n"
                }
        println(out)
    }
}
